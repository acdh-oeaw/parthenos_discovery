DEFINE input:inference 'parthenos_rules'

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX parthenos: <http://parthenos.d4science.org/CRMext/CRMpe.rdfs/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMext/CRMdig.rdfs/>


select distinct 
#?gRecord
( COALESCE(str(?instance_label), ?instanceURI) as ?instance )
( COALESCE(str(?id_label), ?id) as ?ID)
( COALESCE(str(?name_label), ?name) as ?Name )

(GROUP_CONCAT (DISTINCT COALESCE(str(?service), ?service), "\n") as ?Services )
(GROUP_CONCAT (DISTINCT COALESCE(str(?service_label), ?service), "\n") as ?Services_Label )
(GROUP_CONCAT (DISTINCT COALESCE(str(?team_label), ?team), "\n") as ?Team )

(GROUP_CONCAT (DISTINCT CONCAT ( 
STRAFTER( str(?actorRelation), str(crm:)), ": ",
COALESCE(str(?member_label), ?member), 
?delim1,
COALESCE(str(?member_address_type_label), ?member_address_type), 
?delim2, COALESCE(str(?member_address_label), ?member_address)
),"\n") as ?Team_Members)

(GROUP_CONCAT (DISTINCT CONCAT( 
COALESCE(str(?time_span_label), ?time_span), ?delim3, str(?time_actual) 
),"\n") as ?Time_Span_of_Project )




where {

#source
GRAPH <provenance> {?gRecord <dnetcollectedFrom> ?api . ?api <dnetisApiOf> "PARTHENOS"^^<http://www.w3.org/2001/XMLSchema#string>.}

#Gets all Projects per source-record
GRAPH ?gRecord {#

#Gets the Project
?instanceURI a parthenos:PE35_Project .

#Gets the Label of Project
?instanceURI rdfs:label ?instance_label .

#Gets id of Project
OPTIONAL { ?instanceURI crm:P1_is_identified_by ?id . 
 ?id a crm:E42_Identifier; rdfs:label ?id_label. }

#Gets Value of Name of Project
OPTIONAL { ?instanceURI crm:P1_is_identified_by ?name.
 ?name a crm:E41_Appellation; rdfs:label ?name_label. }

#Gets the Service currently offered by Project
OPTIONAL { ?instanceURI parthenos:PP1_currently_offers ?service .
 OPTIONAL { ?service rdfs:label ?service_label. }  }

#Gets the Team currently maintaining Project
OPTIONAL { ?instanceURI parthenos:PP44_has_maintaining_team ?team .
 OPTIONAL { ?team rdfs:label ?team_label.}
 
 #Gets the Members of the Team
 OPTIONAL { ?team ?actorRelation ?member . #crm:P107_has_current_or_former_member
 ?member a crm:E39_Actor.
 
  OPTIONAL { ?member rdfs:label ?member_label. }
 
   #Gets the Address of Actor
   OPTIONAL { ?member crm:P76_has_contact_point ?member_address.
    OPTIONAL { ?member_address rdfs:label ?member_address_label. }
 
    #Gets the type of Address of Actor
    OPTIONAL { ?member_address crm:P2_has_type ?member_address_type.
     OPTIONAL { ?member_address_type rdfs:label ?member_address_type_label. } 
    }#Member_address_type --end
   }#Member_address --end
  }#Member --end
 }#Team --end
 

#Gets the Time since when project has been run
OPTIONAL { ?instanceURI crm:P4_has_time-span ?time_span .
 OPTIONAL { ?time_span rdfs:label ?time_span_label. } 
 OPTIONAL { ?time_span crm:P82a_begin_of_the_begin ?time_actual .}
}

}#?gRecord --end

Bind(if(Bound(?member),", ","") as ?delim1)
Bind(if(Bound(?member_address_type),": ","") as ?delim2)
Bind(if(Bound(?time_actual),": ","") as ?delim3)

}
group by  ?instanceURI ?instance_label ?id ?id_label ?name ?name_label
#order by  ?instance ?ID ?Name

