#6 ############ Get Types related to Classes, per Datasource ###########
#Note: the class labels (i.e. ?class_label, ?instanceClass_label) have been replaced with the class names for better performance#

#DEFINE input:inference 'parthenos_rules'
select  
?type  
(str(?type_label) as ?type_label)
#(replace(str(?type),"(^.*/)",  "", "i" ) as ?type_label)
?class
(replace(str(?class),"(^.*/)",  "", "i" ) as ?class_label)
?instanceClass
(replace(str(?instanceClass),"(^.*/)",  "", "i" ) as ?instanceClass_label)
(COUNT(distinct ?instanceURI) as ?instanceCount)
(str(?ds) as ?ds)
where{

graph ?gRecord  {
?type a ?class.
optional {?type rdfs:label ?type_label .
FILTER ( langmatches(lang(?type_label), "en") || langmatches(lang(?type_label), "") ) 
}
{?instanceURI crm:P2_has_type ?type} union {?type crm:P2i_is_type_of ?instanceURI}
}

values ?classE55 { <http://www.cidoc-crm.org/cidoc-crm/E55_Type> }
?class rdfs:subClassOf* ?classE55 .


optional{ ?instanceURI a ?instanceClass }

GRAPH <provenance> {
values ?ds { "PARTHENOS"^^<http://www.w3.org/2001/XMLSchema#string> }
?gRecord <dnetcollectedFrom> ?api . ?api <dnetisApiOf> ?ds}

} 

group by ?ds ?class ?instanceClass ?type ?type_label
order by ?ds ?type


#---end of query ----

